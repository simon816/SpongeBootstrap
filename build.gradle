apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

version = '0.6.0'

repositories {
    mavenCentral()
    maven {
        name = 'minecraft'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'forge'
        url = 'http://files.minecraftforge.net/maven/'
    }
}

sourceSets {
    lib
}

dependencies {
    compile 'net.minecraft:launchwrapper:1.12'
    compile 'org.apache.logging.log4j:log4j-core:2.0-beta9'
    compile 'net.minecraftforge:forge:1.12.2-14.23.0.2486:universal'
}

project.ext.supportedVersions = ["1.8.9", "1.10.2", "1.11", "1.11.2", "1.12", "1.12.1", "1.12.2"]

task setSupportedVersions(type: Copy) {
    from(sourceSets.main.java.srcDirs)
    into 'build/sources/java'
    filter(org.apache.tools.ant.filters.ReplaceTokens,
            tokens: ['supportedVersions': project.ext.supportedVersions.join(",")])
}

compileJava.source = setSupportedVersions.outputs.files
compileJava.dependsOn setSupportedVersions
compileJava.classpath += sourceSets.lib.output


jar {
    manifest {
        attributes(
            "Main-Class": "com.simon816.sponge.bootstrap.Bootstrap",
            "Class-Path": "libraries/net/minecraft/launchwrapper/1.12/launchwrapper-1.12.jar"
                        + " libraries/net/sf/jopt-simple/jopt-simple/5.0.3/jopt-simple-5.0.3.jar"
                        + " libraries/net/sf/jopt-simple/jopt-simple/4.6/jopt-simple-4.6.jar "
                        + project.ext.supportedVersions.collect{ "minecraft_server.${it}.jar" }.join(" ")
        )
    }
}
